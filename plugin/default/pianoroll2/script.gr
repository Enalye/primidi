/** 
 * Copyright: Enalye
 * License: Zlib
 * Authors: Enalye
 */

// Channels' color
let colors = [
    Color(109, 247, 193),
    Color(17, 173, 193),
    Color(96, 108, 129),
    Color(57, 52, 87),
    Color(30, 136, 117),
    Color(91, 179, 97),
    Color(161, 229, 90),
    Color(247, 228, 118),
    Color(249, 146, 82),
    Color(203, 77, 104),
    Color(106, 55, 113),
    Color(201, 36, 100),
    Color(244, 140, 182),
    Color(247, 182, 158),
    Color(155, 156, 130),
    Color(49, 122, 21)
];

Texture noteTexture;
let barWidth, barFactor = 2f, 0f;
Color barColor;
float barX;
Sprite background;

// Init
main {
    Color baseBarColor;
    baseBarColor, barColor = Color(1f, 1f, 1f);

    setLayerClearColor(-1, Color(0.051, 0.082, 0.125));
    setLayerClearAlpha(-1, 1f);

    /*
        Default interval of ticks to be displayed.
        A too large value can cause slowdowns.
    */
    setInterval(6000);
    /*
        0 = note are hit on the right side of the screen.
        0.5 = in the center.
        1 = left side.
    */
    setRatio(0.8f);

    setLayersCount(17);

    noteTexture = Texture("../note.png");

    loop {
        setLayer(-1);
        if(background)
            background:draw(getCenterX() as float, getCenterY() as float);
        // Middle bar
        if(barFactor > 1f)
            barFactor = 1f;
        setLayer(16);
        barX = getWidth() * (1f - getRatio()) - (barWidth / 2f);
        fillRect(barX, 0f, barWidth, getHeight() as float, lerp(baseBarColor, barColor, barFactor), lerp(0.2f, 0.6f, barFactor));
        if(barFactor < 0f) {
            barFactor = 0f;
        }
        else {
            barFactor -= 0.1f;
        }
        yield
    }
}

event onNoteInput(Note note) {
    let noteSprite = noteTexture as Sprite;
    noteSprite:setAnchor(0f, 0f);

    Color color = colors[note:getChannel];

    let pitch = 1f - (note:getPitch as float / 128f);
    float fadeOut = 1f;
    int time = 0;

    let w, h =
        getWidth() * note:getDuration,
        getHeight() / 128f;
    let particleX = barX + barWidth;

    let x, y =
        getWidth() * (1f - note:getTime),
        getHeight() * pitch - getPitchBend(note:getChannel) * h * 2f;

    loop(i, 17)
        spark(particleX, y + h / 2f, -85f + (10f * i) + rand(-6f, 6f), rand(1.1f, 1.7f), rand(5, 30), color, color);

    loop {
        unless(note:isAlive)
            kill

        w, h =
            getWidth() * note:getDuration,
            getHeight() / 128f;

        noteSprite:setSize(w, h);
            
        x, y =
            getWidth() * (1f - note:getTime),
            getHeight() * pitch - getPitchBend(note:getChannel) * h * 2f;

        if(note:isPlaying) {
            let factor = note:getPlayTime;
            let c = lerp(Color(1f, 1f, 1f), color, factor);
            noteSprite:setColor(c);
            noteSprite:setAlpha(1f);

            // Particles
            if((time % 5) == 0) {
                spark(particleX, y, rand(-85f, 5f), rand(0.2f, 1.5f), rand(5, 20), color, c);
                spark(particleX, y + h, rand(5f, 85f), rand(0.2f, 1.5f), rand(5, 20), color, c);
            }
            barColor = mix(barColor, c);
            barFactor += (1f - factor) * 0.2f;
        }
        else {
            if(note:getPlayTime > 0f) {
                noteSprite:setColor(lerp(Color(1f, 1f, 1f), color, fadeOut));
                noteSprite:setAlpha(fadeOut);
                if(fadeOut > 0f) {
                    fadeOut -= 0.025f;
                    if(fadeOut < 0f) {
                        fadeOut = 0f;
                    }
                }
            }
            else {
                noteSprite:setColor(Color(1f, 1f, 1f));
                noteSprite:setAlpha(0.5f);
            }
        }
        setLayer(note:getChannel);
        noteSprite:draw(x, y);
        time ++;
        yield
    }
}

// Called for each note entering the Interval window.
event onNoteEnter(Note note) {
    bool wasHit = false;
    let noteSprite = noteTexture as Sprite;
    noteSprite:setAnchor(0f, 0f);

    Color color = colors[note:getChannel];

    let pitch = 1f - (note:getPitch as float / 128f);
    float fadeOut = 1f;
    int time = 0;

    loop {
        unless(note:isAlive)
            kill

        let w, h =
            getWidth() * note:getDuration,
            getHeight() / 128f;

        if(w < 4f)
            w = 4f;
        
        noteSprite:setSize(w, h);
        
        let x, y =
            getWidth() * (1f - note:getTime),
            getHeight() * pitch - getPitchBend(note:getChannel) * h * 2f;;

        if(note:isPlaying) {
            let factor = note:getPlayTime;
            let c = lerp(Color(1f, 1f, 1f), color, factor);
            noteSprite:setColor(c);
            noteSprite:setAlpha(1f);

            // Particles
            let particleX = barX + barWidth;
            unless(wasHit) {
                wasHit = true;
                loop(i, 17)
                    spark(particleX, y + h / 2f, -85f + (10f * i) + rand(-6f, 6f), rand(1.1f, 1.7f), rand(5, 30), color, c);
            }
            else if((time % 5) == 0) {
                spark(particleX, y, rand(-85f, 5f), rand(0.2f, 1.5f), rand(5, 20), color, c);
                spark(particleX, y + h, rand(5f, 85f), rand(0.2f, 1.5f), rand(5, 20), color, c);
            }
            barColor = mix(barColor, c);
            barFactor += (1f - factor) * 0.2f;
        }
        else {
            if(note:getPlayTime > 0f) {
                noteSprite:setColor(lerp(Color(1f, 1f, 1f), color, fadeOut));
                noteSprite:setAlpha(fadeOut);
                if(fadeOut > 0f) {
                    fadeOut -= 0.025f;
                    if(fadeOut < 0f) {
                        fadeOut = 0f;
                    }
                }
            }
            else {
                noteSprite:setColor(Color(1f, 1f, 1f));
                noteSprite:setAlpha(0.5f);
            }
        }
        setLayer(note:getChannel);
        noteSprite:draw(x, y);
        time ++;
        yield
    }
}

// Create a single particle
task spark(float x, float y, float angle, float speed, int ttl, Color startColor, Color endColor) {
    let particle = Particle(x, y, angle, speed, ttl);
    unless(particle) kill

    let sprite = particle:getSprite;
    sprite:setBlend(Blend.additive);
    particle:setColor(startColor);
    particle:setAlpha(1f);
    float time = 0f;
    while(particle:isAlive) {
        particle:setColor(lerp(startColor, endColor, time / ttl));
        time ++;
        yield
    }
}

event onFileDrop(string fileName) {
    let tex = Texture(fileName);
    unless(tex) return;
    background = Sprite(tex);
    background:contain(getWidth() as float, getHeight() as float);
}